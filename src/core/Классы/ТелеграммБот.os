#Использовать oint
#Использовать json

Перем Токен;
Перем Смещение;
Перем РазрешенныеПользователи;
Перем РазрешенныеГруппы;
Перем КлавиатураОбщая;
Перем МассивКлавиатуры;
Перем ГрафанаОпрашиватель;
Перем ПомнюДашборды;
Перем ПарсерJSON;

&Желудь
Процедура ПриСозданииОбъекта()

	ГрафанаОпрашиватель = Новый ГрафанаОпрашиватель;	
	ПарсерJSON = Новый ПарсерJSON;
	
	ПомнюДашборды = Новый Соответствие();

	Смещение = 0;
	Токен = "";
	
	РазрешенныеПользователи = Новый Соответствие();
	РазрешенныеПользователи.Вставить(69190503, Истина);
	РазрешенныеПользователи.Вставить(377364329, Истина);

	РазрешенныеГруппы = Новый Соответствие();
	РазрешенныеГруппы.Вставить(-4533618930, Истина);
	РазрешенныеГруппы.Вставить(-1001429542625, Истина);

	МассивКлавиатуры = МассивКлавиатуры();
	КлавиатураОбщая = OPI_Telegram.СформироватьКлавиатуруПоМассивуКнопок(МассивКлавиатуры, Ложь, Истина);
	
КонецПроцедуры

Функция МассивКлавиатуры()
	Возврат ГрафанаОпрашиватель.ИменаПлейлистов(); 
КонецФункции

Процедура Запустить() Экспорт
	Сообщить("Запущен");

	Пока Истина Цикл
	
		Ответ     = OPI_Telegram.ПолучитьОбновления(Токен, 30, Смещение);
		Результат = Ответ["result"];

		Если Результат.ВГраница() = -1 Тогда
			Продолжить;
		Иначе
			Собщение = Результат[Результат.ВГраница()];
			Смещение  = Собщение["update_id"] + 1;
		КонецЕсли;

		ОбработатьРезультат(Собщение);

	КонецЦикла;

КонецПроцедуры

Процедура ОбработатьРезультат(Собщение)

	Если Собщение.Получить("message") <> Неопределено Тогда
		ПользовательИД = Собщение["message"]["from"]["id"];
		ЧатИД = Собщение["message"]["chat"]["id"];
	ИначеЕсли  Собщение.Получить("callback_query") <> Неопределено Тогда
		ПользовательИД = Собщение["callback_query"]["from"]["id"];
		ЧатИД = Собщение["callback_query"]["message"]["chat"]["id"];
	КонецЕсли;

	Сообщить(ЧатИД);
	Если РазрешенныеПользователи[ПользовательИД] = Неопределено 
			И РазрешенныеГруппы[ЧатИД] = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ПоказатьКлавиатуру(Собщение);

	Если НЕ НадоИдтиДальше(Собщение) Тогда
		Возврат;
	КонецЕсли;

	ПоказатьСписокДашбордов(Собщение);

	ОтправитьДашборт(Собщение);

КонецПроцедуры

Функция НадоИдтиДальше(Сообщение)

	Возврат Истина;
КонецФункции
	
Процедура ПоказатьСписокДашбордов(Собщение)

	ЭтоСообщение = Собщение["message"] <> Неопределено;

	Если Не ЭтоСообщение Тогда
		Возврат;
	КонецЕсли;

	ТекстСообщения = Собщение["message"]["text"];

	Если МассивКлавиатуры.Найти(ТекстСообщения) = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Дашборды = ГрафанаОпрашиватель.ДашбордыПлейлиста(ТекстСообщения);
	
	Сообщить(ПарсерJSON.ЗаписатьJSON(Дашборды));

	СписокДашбордов = Новый Массив;
	Для Каждого Дашборд Из Дашборды Цикл

		ОписанеДашборда = ГрафанаОпрашиватель.ОписаниеДашборда(Дашборд["value"]);
		title = ОписанеДашборда["dashboard"]["title"];

		Если title = Неопределено Тогда
			Продолжить;
		КонецЕсли;

		СписокДашбордов.Добавить(title);
		ПомнюДашборды.Вставить(title, ОписанеДашборда);
	КонецЦикла;

	КлавиатураДашборда = OPI_Telegram.СформироватьКлавиатуруПоМассивуКнопок(СписокДашбордов, Истина, Истина);

	ЧатИД = Собщение["message"]["chat"]["id"];
	OPI_Telegram.ОтправитьТекстовоеСообщение(Токен, ЧатИД, "Дашборды", КлавиатураДашборда,  Собщение["message"]["id"]);  
КонецПроцедуры

Процедура ОтправитьДашборт(Собщение)

	ЭтоКалбек = Собщение["callback_query"] <> Неопределено;

	Если НЕ ЭтоКалбек Тогда
		Возврат;
	КонецЕсли;
	
	ИмяДашборда = Собщение["callback_query"]["data"];
	Дашборд = ПомнюДашборды[ИмяДашборда];

	Если Дашборд  = Неопределено Тогда
		Возврат;
	КонецЕсли;

	// Сообщить(ПарсерJSON.ЗаписатьJSON(Дашборд));
	
	ЧатИД = Собщение["callback_query"]["message"]["chat"]["id"];

	OPI_Telegram.ОтправитьТекстовоеСообщение(Токен, ЧатИД, "Ожидайте");  
	
	ПутьККартинке = ГрафанаОпрашиватель.КартинкаДашборда(Дашборд["meta"]["url"]);
	OPI_Telegram.ОтправитьКартинку(Токен, ЧатИД, "", ПутьККартинке, КлавиатураОбщая);

КонецПроцедуры

Процедура ПоказатьКлавиатуру(Собщение)
	
	Попытка
	ЭтоСтарт = Нрег(Собщение["message"]["text"]) = Нрег("/start");
	Исключение
		ЭтоСтарт = Ложь;
	КонецПопытки;

	Если НЕ ЭтоСтарт Тогда
		Возврат;
	КонецЕсли;

	ЧатИД = Собщение["message"]["chat"]["id"];

	OPI_Telegram.ОтправитьТекстовоеСообщение(Токен, ЧатИД, "Список плейлистов доступен в панели кнопок", КлавиатураОбщая);  
КонецПроцедуры

