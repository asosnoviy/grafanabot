#Использовать 1connector

&Деталька(Значение = "GRAFANA.URL", ЗначениеПоУмолчанию = "http://grafana.local")
Перем ПутьГрафаны;

&Деталька(Значение = "GRAFANA.TOKEN", ЗначениеПоУмолчанию = "")
Перем ТокенГрафаны;

&Соответствие
Перем Авторизация;

&Массив
Перем Плейлисты;

&Желудь
Процедура ПриСозданииОбъекта()

КонецПроцедуры

&ФинальныйШтрих
Процедура ПослеСозданииОбъекта() Экспорт

	Если ЗначениеЗаполнено(ТокенГрафаны) Тогда
		Авторизация.Вставить("Authorization", "Bearer " + ТокенГрафаны);
	КонецЕсли;

	Попытка
		Плейлисты = КоннекторHTTP.Get(
				ПутьГрафаны + "/api/playlists",
				, 
				Новый Структура("Заголовки", Авторизация)
			).Json();
	Исключение
		Ошибка = Новый Соответствие();
		Ошибка.Вставить("name", "Ошибка");
		Плейлисты.Добавить(Ошибка);
	КонецПопытки;

КонецПроцедуры

Функция ОписаниеДашборда(Uid) Экспорт
	Возврат КоннекторHTTP.Get(
			ПутьГрафаны + "/api/dashboards/uid/" + Uid,
			, 
			Новый Структура("Заголовки", Авторизация)
		).json();
КонецФункции

Функция КартинкаДашборда(url) Экспорт

	ПутьККартинкеОшибки = ОбъединитьПути(ТекущийКаталог(), "fixtures", "error.png");

	Если url = Неопределено Тогда
		Возврат  Новый ДвоичныеДанные(ПутьККартинкеОшибки);
	КонецЕсли;

	ПутьНаСервере = ПутьГрафаны + "/render" + url + "?orgId=1&width=1920&height=1080";
	РезультатДД = КоннекторHTTP.Get(
			ПутьНаСервере, 
			, 
			Новый Структура("Заголовки", Авторизация)
		).ДвоичныеДанные();

	Если РезультатДД.Размер() = 0 Тогда
	 	РезультатДД = Новый ДвоичныеДанные(ПутьККартинкеОшибки);
	КонецЕсли;

	Возврат РезультатДД;
КонецФункции

Функция ИменаПлейлистов() Экспорт
	
	Результат = Новый Массив;
	
	Для Каждого ПлейЛист Из Плейлисты Цикл
		Результат.Добавить(ПлейЛист["name"]);
	КонецЦикла;

	Возврат Результат;
КонецФункции

Функция ИдПлейлиста(ИмяПлейлиста)

	Для Каждого Плейлист Из Плейлисты Цикл
		Если Плейлист["name"] = ИмяПлейлиста Тогда
			Возврат Плейлист["uid"]; 
		КонецЕсли;
	КонецЦикла;

	Возврат -1;
КонецФункции

Функция ДашбордыПлейлиста(ИмяПлейлиста) Экспорт
	
	ОписаниеПлейлиста = КоннекторHTTP.Get(
			ПутьГрафаны + "/api/playlists/" + ИдПлейлиста(ИмяПлейлиста),
			, 
			Новый Структура("Заголовки", Авторизация)
		).json();

	Возврат ОписаниеПлейлиста["items"];
КонецФункции
