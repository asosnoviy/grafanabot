#Использовать 1connector
#Использовать json

Перем ПутьГрафаны;
Перем Авторизация;
Перем Плейлисты;
Перем ПарсерJSON;

&Желудь
Процедура ПриСозданииОбъекта()

	ПутьГрафаны = "http://grafana.domain.local:3000";
	Авторизация = Новый Соответствие();
	Авторизация.Вставить(
			"Authorization", 
			"Bearer "
	);

	Плейлисты = КоннекторHTTP.Get(ПутьГрафаны + "/api/playlists").Json();

	ПарсерJSON = Новый ПарсерJSON;

КонецПроцедуры

Функция ОписаниеДашборда(Uid) Экспорт
	Возврат КоннекторHTTP.Get(ПутьГрафаны + "/api/dashboards/uid/" + Uid).json();
КонецФункции

Функция КартинкаДашборда(url) Экспорт
	
	ПутьККартинкеОшибки = "E:\DEVELOP\Oscript\grafanaBot\fixtures\error.png";
	ПутьККартинке = "E:\DEVELOP\Oscript\grafanaBot\fixtures\tmp.jpg";

	Если url = Неопределено Тогда
		Возврат ПутьККартинке;
	КонецЕсли;

	ПутьНаСервере = ПутьГрафаны + "/render" + url + "?orgId=1&width=1920&height=1080";
	РезультатДД = КоннекторHTTP.Get(ПутьНаСервере).ДвоичныеДанные();

	Если РезультатДД.Размер() > 0 Тогда
		РезультатДД.Записать(ПутьККартинке);
	Иначе
		ПутьККартинке = ПутьККартинкеОшибки;
	КонецЕсли;

	Возврат ПутьККартинке;
КонецФункции

Функция ИменаПлейлистов() Экспорт
	
	Результат = Новый Массив;
	
	Для Каждого ПлейЛист Из Плейлисты Цикл
		Результат.Добавить(ПлейЛист["name"]);
	КонецЦикла;

	Возврат Результат;
КонецФункции

Функция ИдПлейлиста(ИмяПлейлиста)

	Для Каждого Плейлист Из Плейлисты Цикл
		Если Плейлист["name"] = ИмяПлейлиста Тогда
			Возврат Плейлист["uid"]; 
		КонецЕсли;
	КонецЦикла;

	Возврат -1;
КонецФункции

Функция ДашбордыПлейлиста(ИмяПлейлиста) Экспорт
	
	ОписаниеПлейлиста = КоннекторHTTP.Get(ПутьГрафаны + "/api/playlists/" + ИдПлейлиста(ИмяПлейлиста)).json();

	Возврат ОписаниеПлейлиста["items"];
КонецФункции
